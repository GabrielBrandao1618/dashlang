quote                = _{ "\"" }
text                 =  { (!("\"") ~ ANY)* }
string               =  { quote ~ text ~ quote }
float                = ${ "-"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
int                  = ${ "-"? ~ ASCII_DIGIT+ }
boolean              =  { "true" | "false" }
keyword              =  { "return" }
symbol               = @{ !(keyword | boolean) ~ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
asignment_expression =  { symbol ~ "=" ~ (expression | value) }
binary_operator      =  { "+" | "-" | "*" | "/" | ">=" | "<=" | "==" | ">" | "<" | "||" | "&&" }
sub_expression       = _{ "(" ~ expression ~ ")" }
binary_expression    =  { (value | symbol | sub_expression) ~ (binary_operator ~ (value | sub_expression | expression))+ }
call_arg             =  { expression | value }
call_expression      =  { symbol ~ "(" ~ (call_arg ~ ("," ~ call_arg)*)? ~ ")" }
expression           =  { binary_expression | asignment_expression | call_expression | symbol }
return_stmt          =  { "return" ~ (expression | value) }
scope                =  { "{" ~ program ~ "}" }
else_stmt            =  { "else" ~ scope }
if_stmt              =  { "if" ~ (expression | value) ~ scope ~ else_stmt? }
while_stmt           =  { "while" ~ (expression | value) ~ scope }
print_stmt           =  { "print" ~ (expression | value) }
statement            =  { return_stmt | if_stmt | while_stmt | print_stmt }
instruction          =  { statement | expression }
closure_param        =  { symbol }
closure_params       =  { closure_param ~ ("," ~ closure_param)* }
closure              =  { "(" ~ closure_params? ~ ")" ~ scope }
value                =  { string | float | int | boolean | closure }

program = { instruction* }

WHITESPACE = _{ " " | "\n" }
